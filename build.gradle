
description = 'upos is a lightweight ISO8583 message sdk.'

repositories { mavenCentral() }

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'findbugs'

group='com.rockagen'
version = '0.1.0-RELEASE'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.repoUrl =isReleaseVersion?releaseUrl:snapshotUrl

compileJava {
	sourceCompatibility=1.6
	targetCompatibility=1.6
	excludes ["**/package-info.java"]
}

compileTestJava {
	sourceCompatibility=1.7
	targetCompatibility=1.7
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
	ignoreFailures=true
	exclude "**/Test*"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

jar {
	manifest {
		attributes 'Implementation-Title': rootProject.name,
		'Implementation-Version': version,
		'Implementation-Vendor': 'rockagen.com',
		'Implementation-Vendor-Id':'com.rockagen',
		'Built-JDK': "${System.getProperty('java.runtime.name')} (${System.getProperty('java.version')})",
		"Built-At": new Date().format("yyyy-MM-dd HH:mm:ss")
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}


signing {
	if(isReleaseVersion)
	sign configurations.archives
}

configurations { deployerJars }

dependencies { deployerJars "org.apache.maven.wagon:wagon-http:2.2" }

dependencies {
	compile group: "com.rockagen", name: "commons-lib", version: "0.1.4"
	compile group: "org.bouncycastle",name: "bcprov-jdk16",version: "1.46"
	testCompile group: "junit", name: "junit", version: "4.8.2"
}

test { systemProperties 'upos': 'youpos' }

//---------------------------------
uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			beforeDeployment { MavenDeployment deployment -> if (isReleaseVersion) signing.signPom(deployment)}
			repository(url: repoUrl) {
				authentication(userName: username, password: password)
			}
			pom.project{
				name rootProject.name
				description rootProject.description
				url 'https://github.com/rockagen/upos'
				inceptionYear '2013'
				organization {
					name 'ROCKAGEN'
					url 'ROCKAGEN.COM'
				}
				scm {
					url 'https://github.com/rockagen/upos'
					connection 'scm:git:git://github.com/rockagen/upos.git'
					developerConnection 'scm:git:ssh://git@github.com/rockagen/upos.git'
				}
				licenses {
					license {
						name 'Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0'
					}
				}
				developers {
					developer {
						id 'rockagen.com'
						name 'The ROCKAGEN Project Contributors'
						email 'ra@rockagen.com'
						url 'http://rockagen.com'
						organization 'ROCKAGEN'
						organizationUrl 'http://rockagen.com'
					}
				}
			}

		}
	}
}



